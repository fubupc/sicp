(define (make-sum . s)
  (define (sum-iter num-sum symbols seq)
    (if (null? seq)
      (cond ((null? symbols) num-sum)
            ((= num-sum 0) 
             (if (= 1 (length symbols))
               (car symbols)
               (cons '+ symbols)))
            (else (cons '+ (cons num-sum symbols))))
      (let ((next (car seq))
            (rest (cdr seq)))
        (if (number? next)
          (sum-iter (+ num-sum next) symbols rest)
          (sum-iter num-sum (append symbols (list next)) rest)))))
  (sum-iter 0 '() s))

(define (addend e)
  (cadr e))

(define (augend e)
  (let ((_aug (cddr e)))
    (if (null? (cdr _aug))
      (car _aug)
      (cons '+ _aug))))

(define (make-product . s)
  (define (mul-iter num-product symbols seq)
    (if (null? seq)
      (cond ((null? symbols) num-product)
            ((= num-product 1)
             (if (= 1 (length symbols))
               (car symbols)
               (cons '* symbols)))
            (else (cons '* (cons num-product symbols))))
      (let ((next (car seq))
            (rest (cdr seq)))
        (cond ((and (number? next) (= next 0)) 0)
              ((number? next) (mul-iter (* num-product next) symbols rest))
              (else (mul-iter num-product (append symbols (list next)) rest))))))
  (mul-iter 1 '() s))

(define (multiplier e)
  (cadr e))

(define (multiplicand e)
  (let ((_mul (cddr e)))
    (if (null? (cdr _mul))
      (car _mul)
      (cons '* _mul))))

